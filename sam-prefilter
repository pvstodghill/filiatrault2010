#! /usr/bin/env perl

# 1. Remove reads that do not appear to align with any of the target
# replicons.
#
# 2. Ensure that all instances of QNAME appear contiguously.

use strict;
use warnings;

use Getopt::Std;

our $opt_h;

if (!getopts("h")) {
  usage(1);
}
if ($opt_h) {
  usage(0);
}
sub usage {
  print STDERR "Usage: $0 options < input.sam > output.sam\n";
  print STDERR "-h - this message\n";
  exit(@_);
}


my %size;
my %seen;
my $curr_qname;

while (<STDIN>) {
  chomp;
  if ( /^\@SQ\t/ ) {
    ( /^\@SQ\tSN:([^\t]+)\tLN:([0-9]+)/ ) || die "<<$_>>\n";
    my ($n,$l) = ($1,$2);
    $size{$n} = $l;
  } elsif ( /^\@/ ) {
    # nothing
  } else {
    my ($qname,$flag,$rname,$pos,$mapq,$cigar) = split(/\t/);
    # Check #1.
    if (!defined($size{$rname})) {
      next;
    }
    # Check #2.
    if ( !defined($curr_qname) || $qname ne $curr_qname ) {
      if ( $seen{$qname} ) {
	die "$qname appears more than once,";
      }
      $seen{$qname} = 1;
      $curr_qname = $qname;
    }
  }
  print "$_\n";
}
